# Setup bash profile by loading *.shell files

# Declare environmental variables needed to setup
export DOTS=$HOME/dev/dotfiles

#Load oh-my-zsh settings
source $DOTS/zsh/oh-my-zsh.sh

# all of our dotfiles
typeset -U config_files
config_files=($DOTS/**/*.shell)

# load the path files
for file in ${(M)config_files:#*/path.shell}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.shell}:#*/completion.shell}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.shell}
do
  source $file
done

unset config_files


# activate direnv
eval "$(direnv hook zsh)"


# The next line updates PATH for the Google Cloud SDK
if [ -f $HOME'/dev/google-cloud-sdk/path.zsh.inc' ]; then source $HOME'/dev/google-cloud-sdk/path.zsh.inc'; fi
# The next line enables shell command completion for gcloud.
if [ -f $HOME'/dev/google-cloud-sdk/completion.zsh.inc' ]; then source $HOME'/dev/google-cloud-sdk/completion.zsh.inc'; fi


show_virtual_env() {
  if [ -n "$CONDA_DEFAULT_ENV" ]; then
    if [[ "$PS1" != *"($(basename $CONDA_DEFAULT_ENV))"* ]];then
        echo "($(basename $CONDA_DEFAULT_ENV))"
    fi
  elif [ -n "$VIRTUAL_ENV" ]; then
    if [[ "$PS1" != *"($(basename $VIRTUAL_ENV))"* ]];then
        echo "($(basename $VIRTUAL_ENV))"
    fi
  fi
}

PS1='$(show_virtual_env) '$PS1