# Setup bash profile by loading *.shell files

# Declare environmental variables needed to setup
export DOTS=$HOME/dev/dotfiles

#Load oh-my-zsh settings
source $DOTS/zsh/oh-my-zsh.sh

# all of our dotfiles
typeset -U config_files
config_files=($DOTS/**/*.shell)

# load the path files
for file in ${(M)config_files:#*/path.shell}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.shell}:#*/completion.shell}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.shell}
do
  source $file
done

unset config_files

# direnv hook
eval "$(direnv hook zsh)"

# restore ps1 conda functionality for direnv
show_env() {
    # if conda env var is set
    if [ -n "$CONDA_DEFAULT_ENV" ]; then
        # and ps1 does not already contain the env name
        if [[ "$PS1" != *"$CONDA_DEFAULT_ENV"* ]]; then
            echo "($CONDA_DEFAULT_ENV)"
        fi
    fi
}

PS1='$(show_env)'$PS1